# Distributed LLM Inference — build & run tasks

llama_dir := env("MESH_LLM_LLAMA_DIR", ".deps/llama.cpp")
llama_build_root := env("MESH_LLM_LLAMA_BUILD_ROOT", ".deps/llama-build")
mesh_dir := "crates/mesh-llm"
ui_dir := "crates/mesh-llm-ui"
# Product source layout: the reviewed extraction relocates the website to
# mesh/website. CI sets MESH_LLM_WEBSITE_DIR from the directory
# .github/actions/resolve-source-layout resolved; otherwise the legacy
# directory wins and a relocated-only checkout falls back to mesh/website.
website_dir := env("MESH_LLM_WEBSITE_DIR", if path_exists(justfile_directory() / "website") == "true" { "website" } else if path_exists(justfile_directory() / "mesh/website") == "true" { "mesh/website" } else { "website" })
home_dir := if os_family() == "windows" { env("USERPROFILE") } else { env("HOME") }
xdg_cache_dir := env("XDG_CACHE_HOME", home_dir / ".cache")
hf_home := env("HF_HOME", xdg_cache_dir / "huggingface")
models_dir := env("HF_HUB_CACHE", hf_home / "hub")
model := models_dir / "GLM-4.7-Flash-Q4_K_M.gguf"

# Match pinned llama.cpp Apple release builds. The variable is macOS-specific;
# native non-Apple toolchains ignore it, and SDK recipes scope their own targets.
macos_deployment_target := env("MACOSX_DEPLOYMENT_TARGET", "")
export MACOSX_DEPLOYMENT_TARGET := if macos_deployment_target == "" { trim(read(justfile_directory() / "scripts/lib/macos-deployment-target.txt")) } else { macos_deployment_target }

# Build for the current platform.
default: build

import 'just/build.just'

import 'just/release-build.just'

import 'just/skippy.just'

import 'just/mesh.just'

import 'just/release-bundle.just'

import 'just/website-ui.just'

import 'just/ci.just'

import 'just/mesh-client.just'

import 'just/utilities.just'

import 'just/desktop.just'
